class TodoList {
  class Todo {
    let title
    let complete = false

    func constructor(title) {
      self.title = title
    }

    func toggle() {
      self.complete = !self.complete
    }
  }

  let todos = []

  func add(title) {
    let todo = Todo(title)
    todos.push(todo)
  }

  func remove(id) {
    todos.delete(id)
  }

  func toggle(id) {
    let todo = todos[id]
    if (todo) {
      todo.toggle()
    }
  }
}

let myList = TodoList()

# input loop

let done = false
let input
while (!done) {

  # Render the todolist
  print("Your Todos:")
  myList.todos.each(func(todo, index) {

    # Get the correct icon
    let icon = "✓".colorize(32)
    if (!todo.complete) {
      icon = "✗".colorize(31)
    }

    # Print the todo
    print(index.colorize(34) + ": " + icon + " " + todo.title)
  })
  print("")

  # Show the menu
  print("
1. Add todo
2. Toggle todo
3. Delete todo
4. Quit application
  ".trim())

  # Get user input
  input = "> ".promptc().to_n()
  write("   ", "\r")

  if (input == 1) {
    myList.add("title > ".prompt().trim())
  } else if (input == 2) {
    myList.toggle("id > ".prompt().to_n())
  } else if (input == 3) {
    myList.remove("id > ".prompt().to_n())
  } else if (input == 4) {
    done = true
  } else {
    print("Sorry, I don't know that command!")
  }

  print("")
}
