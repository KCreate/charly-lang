const array_insert = __internal__method("array_insert")
const array_delete = __internal__method("array_delete")
const array_of_size = __internal__method("array_of_size")

export = primitive class Array {

  # Calls the callback with each element and the index
  func each(callback) {
    let i = 0
    while i < @length() {
      callback(self[i], i)
      i += 1
    }
    self
  }

  # Calls the callback with each element and the index
  # The return value is an array of return values from the callback
  func map(callback) {
    let new = []
    @each(func(e, i) {
      new.push(callback(e, i))
    })
    new
  }

  # Returns a new array whichs contains all
  # elements for which the passed block returned a truthy value
  func filter(callback) {
    let new = []
    @each(func(e, i) {
      if (callback(e, i)) {
        new.push(e)
      }
    })
    new
  }

  # Returns true if this array is empty
  func empty() {
    @length() == 0
  }

  # Returns an array where all children are turned into strings
  func all_to_s() {
    @map(func(e) {
      e.to_s()
    })
  }

  # Append an item to this array
  func push(item) {
    array_insert(self, item, @length())
    self
  }

  # Inserts *item* at *index*
  func insert(item, index) {
    array_insert(self, item, index)
    self
  }

  # Deletes the item at *index*
  func delete(index) {
    array_delete(self, index)
    self
  }

  # Returns the string representation of this array
  func to_s() {
    let io = "["
    let amount = @length()
    @each(func(e, i) {
      io += e.to_s()

      if i ! amount - 1 {
        io += ", "
      }
    })
    io += "]"
    io
  }

  # Return a new array of a given size
  func of_size(size) {
    array_of_size(size)
  }
}
